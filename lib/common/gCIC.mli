(** This module specifies the structure of GCIC *)
open Id

(** Terms in GCIC *)
type term =
  | Var of Name.t
  | Universe of int
  | App of term * term
  | Lambda of fun_info
  | Prod of fun_info
  | Unknown of int
  (* Inductives *)
  | Inductive of Name.t * int * term list
  | Constructor of Name.t * term list
  | Match of
      { ind : Name.t
      ; discr : term
      ; z : Name.t
      ; pred : term
      ; f : Name.t
      ; branches : branch list
      }
  (* Extras *)
  | Ascription of term * term
  | UnknownT of int
  | Const of Name.t
  | Fixpoint of fix_info

and fun_info =
  { id : Name.t
  ; dom : term
  ; body : term
  }

and branch =
  { ctor : Name.t
  ; ids : Name.t list
  ; term : term
  }

and fix_info =
  { fix_id : Name.t
  ; fix_body : term
  ; fix_type : term
  ; fix_rarg : int (* index of the recursive argument *)
  }

(** Pretty printers *)
val pp_term : Format.formatter -> term -> unit

(** Returns the prettified version of a term *)
val to_string : term -> string

(** Prints the prettified version of a term *)
val print : term -> unit

(** Equality predicate between terms *)
val eq : term -> term -> bool

(** Gets the level of a universe or a product.
    Raises an error if applied on something else. *)
val get_universe_lvl : term -> int

(** Returns the list of arguments of a product type. *)
val prod_args : term -> Name.t list
